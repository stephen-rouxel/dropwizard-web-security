// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the brightSPARK Labs plugin for standardisation.
    id 'com.brightsparklabs.gradle.baseline' version "4.3.0"

    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description  = 'Bundle for applying default web security functionality to a dropwizard application'
project.ext.url      = 'https://github.com/brightsparklabs/dropwizard-web-security'
project.ext.scm      = 'scm:git@github.com:brightsparklabs/dropwizard-web-security.git'

// -----------------------------------------------------------------------------
// SETUP BASELINE PLUGIN
// -----------------------------------------------------------------------------

bslBaseline {
    licenseHeader = """/*
                      | * (c) Copyright 2016-2017 Palantir Technologies Inc. All rights reserved.
                      | *
                      | * (c) Copyright 2023 brightSPARK Labs (from commit `c2774cac049bb0007d14790527ea2499670fef83` onwards).
                      | * All rights reserved.
                      | */
        """.stripMargin("|")
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    // Use mavenCentral for resolving dependencies.
    mavenCentral()
}

dependencies {
    def versions = [
        dropwizard: '4.0.4',
        immutables: '2.9.3',
    ]
    // These dependencies are found on compile classpath of this component and consumers.
    implementation(
            // General
            'com.google.guava:guava:31.1-jre',

            // Dropwizard
            "io.dropwizard:dropwizard-core:${versions.dropwizard}",
            )

    compileOnly(
            "org.immutables:value:${versions.immutables}",
            // We add this so that Immutables will annotate its generated classes with
            // @SuppressFBWarnings, and thus avoid Findbugs errors for those generated classes.
            'com.google.code.findbugs:annotations:3.0.1'
            )

    testImplementation(
            "io.dropwizard:dropwizard-assets:${versions.dropwizard}",
            "io.dropwizard:dropwizard-client:${versions.dropwizard}",
            "io.dropwizard:dropwizard-testing:${versions.dropwizard}",
            'org.junit.jupiter:junit-jupiter-api:5.10.1',
            'org.mockito:mockito-core:5.7.0',
            'org.springframework:spring-test:6.1.0',
            'jakarta.validation:jakarta.validation-api:3.0.2',
            )

    annotationProcessor(
            "org.immutables:value:${versions.immutables}",
            )
}
